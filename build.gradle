plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.5.20'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.5.20'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'cyou.untitled.restfulbsg'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    flatDir {
        dirs 'libs'
    }
}

shadowJar {
    dependencies {
        exclude(dependency('org.jetbrains.kotlin.*:.*:.*'))
    }

    archiveBaseName.set('RESTful-BSG')
    archiveVersion.set(project.version as String)
    zip64 true
    relocate('org.bstats', 'cyou.untitled.restfulbsg.bstats')
    relocate('com.typesafe', 'cyou.untitled.restfulbsg.typesafe')
}

dependencies {
    // Library loading is delegated to BungeeCord
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib"
    compileOnly 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1'
    compileOnly 'net.md-5:bungeecord-api:1.17-R0.1-SNAPSHOT'
    compileOnly name: 'BungeeSafeguard-3.0-SNAPSHOT-all'
    // Ktor server
    // 1.6.1 is currently broken due to incorrect class loader
    compileOnly "io.ktor:ktor-server-core:1.6.0"
    compileOnly "io.ktor:ktor-server-netty:1.6.0"
    compileOnly "io.ktor:ktor-serialization:1.6.0"
    compileOnly "ch.qos.logback:logback-classic:1.2.3"
    // bStats
    implementation 'org.bstats:bstats-bungeecord:2.2.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}